apply plugin: 'application'

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'assets/**'
    from('src/main/resources/assets') { into '' }
}
dependencies {
    implementation project(":core")
    implementation project(":server")

    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.box2dlights:box2dlights:$box2dLightsVersion"
    implementation "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
    runtimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

application {
    mainClass = 'net.lapidist.colony.client.ClientLauncher'
    // macOS requires starting on the first thread when using GLFW
    if (System.getProperty('os.name').toLowerCase().contains('mac')) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

eclipse.project.name = "$appName-client"
